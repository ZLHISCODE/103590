VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
  Persistable = 0  'NotPersistable
  DataBindingBehavior = 0  'vbNone
  DataSourceBehavior  = 0  'vbNone
  MTSTransactionMode  = 0  'NotAnMTSObject
END
Attribute VB_Name = "clsLISDev_VIT_250"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = True
Attribute VB_PredeclaredId = False
Attribute VB_Exposed = True
Option Explicit
'2009-04-09 陈东修改
Private mstrCode As String
Public Function GetStartCmd() As String
    '设备标准接口
    '功能：获取设备开始发送命令
    GetStartCmd = ""
End Function

Public Function GetEndCmd() As String
    '设备标准接口
    '功能：获取设备停止发送命令
    GetEndCmd = ""
End Function

Public Sub Analyse(ByVal strSource As String, strResult As String, strReserved As String, _
    strCmd As String)
    '设备标准接口
    '功能：解析数据
    '参数：
    'strSource：要解析的原始串
    'strResult：返回的检验结果(各仪器解析程序必须按以下标准组织结果)
    '   每组检验结果以||分隔,元素之间以|分隔
    '   第0个元素：检验时间
    '   第1个元素：样本序号
    '   第2个元素：检验人
    '   第3个元素：标本
    '   第4个元素：是否质控品
    '   从第5个元素开始为检验结果，每2个元素表示一个检验项目。
    '       如：第5i个元素为检验项目，第5i+1个元素为检验结果
    'strReserved：最后不能完成解析的原始串，需要返回作后续处理
    'strCmd：如果需要，可返回向设备发送的命令
    
    Dim iStrPoint As Long, iStrPointBK As Integer '字符指针
    Dim strBuffer As String, strData As String
    Dim iCurrGrpEntry As Integer '当前这组结果的入口字符位置
    
    Dim blnReadItem As Boolean '当前是否正在读取检验项目
    
    Dim sng1 As Single, sng2 As Single, iPos As Long
    Dim blnQc As Boolean
    Dim aItemData() As String
    Dim i As Long
    Dim strCode As String, lng_S As Long, lng_E As Long
    '初始化返回值
    strReserved = "": strResult = "": strCmd = "": blnQc = False
    '收到一行应答
    'If InStr(strSource, Chr(&HA)) > 0 Then strCmd = Chr(&H6)
    
    If strSource <> "" Then mstrCode = mstrCode & strSource
    
    lng_S = InStr(mstrCode, "!000a")
    If lng_S > 0 Then
        lng_E = InStr(lng_S, mstrCode, "h")
        If lng_E > 0 Then
            If Mid(mstrCode, lng_E - 4, 1) = "!" Then
                lng_E = InStr(lng_E, mstrCode, Chr(&HA))
            Else
                lng_E = 0
            End If
        End If
    Else
        lng_E = 0
    End If
    Do While lng_S > 0 And lng_E > 0
        strCode = Mid(mstrCode, lng_S, lng_E - lng_S + 1)
        mstrCode = Mid(mstrCode, lng_E + 1)
        WriteLog "V250 解析:", strCode, ""
    '----------------------------------------------------------------------
        iStrPoint = 1: blnReadItem = False
        Do While iStrPoint <= Len(strCode)
            If Not blnReadItem Then '当前是否正在读取检验项目
                '查找信息的开始位置
                iPos = iStrPoint
                strBuffer = LTrim(ReadLine(strCode, iPos))
                If strBuffer Like "*!000*" Then
                    '记录一组结果的入口
                    iCurrGrpEntry = iStrPoint
                    '数据是否完整
                    If ifEnd(strCode, iStrPoint) Then
                        '信息完整，不再保留
                        iCurrGrpEntry = 0
                        
                        iStrPoint = iPos
                        aItemData = Split(strBuffer, " ")
                        '检验时间
                        '时间格式必须为：yyyy-mm-dd hh:mm:ss
                        strData = Format(Mid(aItemData(2), 7, 2) & "-" & Mid(aItemData(2), 9, 2) & "-" & _
                            Mid(aItemData(2), 11, 2), "yyyy-mm-dd") & " " & Mid(aItemData(2), 1, 2) & ":" & _
                            Mid(aItemData(2), 3, 2) & ":00"
                                
                        '样本序号，必须为数字
                        strData = strData & "|" & Val(Mid(aItemData(2), 13))
                                
                        '检验人
                        strData = strData & "| " '本仪器不支持
                        '标本
                        strData = strData & "|血液"
                        '质控品
                        strData = strData & "|0"
                    
                        '准备接收
                        blnReadItem = True
                    Else
                        Exit Do
                    End If
                Else
                    iStrPoint = iPos
                End If
            Else
                strBuffer = LTrim(ReadLine(strCode, iStrPoint))
                If Len(strBuffer) > 0 Then
                    If InStr(strBuffer, " ") > 0 Then '检验结果
                        aItemData = Split(strBuffer, " ")
                        iPos = InStr(aItemData(0), "!")
                        strData = strData & "|" & UCase(Mid(aItemData(0), iPos + 5, 4))
                        If InStr("+-", Right(strData, 1)) > 0 Then strData = Mid(strData, 1, Len(strData) - 1)
                        If Len(aItemData(0)) > 10 Then
                            strData = strData & "|" & Val(Mid(aItemData(0), 11))
                        Else
                            strData = strData & "|" & Val(Mid(strBuffer, Len(aItemData(0)) + 1))
                        End If
                    Else
                        '一个标本结束
                        strResult = IIf(Len(strResult) > 0, strResult & "||", "") & strData
                        blnReadItem = False
                    End If
                End If
            End If
        Loop
    '----------------------------------------------------------------------
        lng_S = InStr(mstrCode, "!000a")
        If lng_S > 0 Then
            lng_E = InStr(lng_S, mstrCode, "h")
            If lng_E > 0 Then
                If Mid(mstrCode, lng_E - 4, 1) = "!" Then
                    lng_E = InStr(lng_E, mstrCode, Chr(&HA))
                Else
                    lng_E = 0
                End If
            End If
        Else
            lng_E = 0
        End If
    Loop
    
End Sub

Public Function SendSample(ByVal strSample As String, iSendStep As Integer, blnSuccess As Boolean, Optional ByVal strResponse As String = "") As String
    '解析需要向仪器发送的标本信息
    '   标本之间以||分隔
    '   元素之间以|分隔
    '   第0个元素：检验时间
    '   第1个元素：样本序号
    '   第2个元素：检验人
    '   第3个元素：标本
    '   第4个元素：是否质控品
    '   第5～9元素：系统保留
    '   从第10个元素开始为需要的检验项目。
    SendSample = ""
End Function

Private Function ReadSubString(ByVal strSource As String, iPoint As Long, Optional ByVal Length As Integer = 0) As String
    '读取子串，同时指针移动到读取的字符串之后
    On Error Resume Next
    If Length = 0 Then
        ReadSubString = Mid(strSource, iPoint)
        iPoint = Len(strSource) + 1
    Else
        ReadSubString = Mid(strSource, iPoint, Length)
        iPoint = iPoint + Length
    End If
End Function

Private Function ReadLine(ByVal strSource, iPoint As Long) As String
    '从指定位置开始读取一行，同时指针定位到下一个换行之后
    Dim iPos As Long
    
    iPos = iPoint
    Do While iPos <= Len(strSource)
        If Mid(strSource, iPos, 1) = Chr(13) Then
            Exit Do
        End If
        iPos = iPos + 1
    Loop
    If iPos > Len(strSource) Then
        ReadLine = Mid(strSource, iPoint)
        iPoint = iPos
    Else
        ReadLine = Mid(strSource, iPoint, iPos - iPoint)
        iPoint = iPos + 1
    End If
End Function

Private Function ifEnd(ByVal strSource, ByVal lngBeginPos As Long) As Boolean
    '查找结果串是否发送完：看最后一行是否有空格
    Dim iPos As Long, strTmp As String
    
    ifEnd = False
    For iPos = Len(strSource) - 1 To lngBeginPos Step -1
        If Mid(strSource, iPos, 1) = Chr(13) Then Exit For
    Next
    If iPos < lngBeginPos Then
        Exit Function
    Else
        strTmp = Mid(strSource, iPos + 1)
        If Len(strTmp) > 0 And InStr(strTmp, " ") = 0 Then ifEnd = True
    End If
End Function
