VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
  Persistable = 0  'NotPersistable
  DataBindingBehavior = 0  'vbNone
  DataSourceBehavior  = 0  'vbNone
  MTSTransactionMode  = 0  'NotAnMTSObject
END
Attribute VB_Name = "clsLISDev_MEDONIC_M20M"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = True
Attribute VB_PredeclaredId = False
Attribute VB_Exposed = True
Option Explicit

'仪器:MEDONIC M20 血细胞分析仪
'厂商:杭州麦道科技有限公司
'作者:李建飞
'用户:陈辉
'日期:2010-01-29

Public Function GetStartCmd() As String
    '设备标准接口
    '功能：获取设备开始发送命令
    GetStartCmd = ""
End Function

Public Function GetEndCmd() As String
    '设备标准接口
    '功能：获取设备停止发送命令
    GetEndCmd = ""
End Function

Public Sub Analyse(ByVal strSource As String, strResult As String, strReserved As String, _
    strCmd As String)
    '设备标准接口
    '功能：解析数据
    '参数：
    'strSource：要解析的原始串
    'strResult：返回的检验结果(各仪器解析程序必须按以下标准组织结果)
    '   每组检验结果以||分隔,元素之间以|分隔
    '   第0个元素：检验时间
    '   第1个元素：样本序号 后面加 ^1 表示急诊 后加加 ^条码
    '   第2个元素：检验人
    '   第3个元素：标本
    '   第4个元素：是否质控品
    '   从第5个元素开始为检验结果，每2个元素表示一个检验项目。
    '       如：第5i个元素为检验项目，第5i+1个元素为检验结果
    'strReserved：最后不能完成解析的原始串，需要返回作后续处理
    'strCmd：如果需要，可返回向设备发送的命令
    
    '请在这个过程中写解析程序，上面的注释行请不要删除。
    Dim str_时间 As String, str_样本号 As String, str_标本 As String
    Dim str_项目 As String, str_结果 As String
    Dim lng_S As Long, lng_E As Long, str_Tmp As String
    Dim str_Data As String, str_Img As String, strImgData As String
    Dim varline As Variant, blnEnd As Boolean
    Dim i As Integer, str_Line As String
    Dim str_smpInfo As String, str_smpresults As String
    Dim str_hgrams As String, str_hgram As String
    Dim lngLength As Long
    
    '初始化返回值
    strResult = "": strReserved = "": strCmd = ""
    
    If strSource Like "*<sample>*</sample>*" Then
        blnEnd = False
        strReserved = strSource
        
        Do While Not blnEnd
            lngLength = InStr(strReserved, "</sample>") + 9 - InStr(strReserved, "<sample>")
            str_Data = Mid(strReserved, InStr(strReserved, "<sample>"), lngLength)
            strReserved = Mid(strReserved, InStr(strReserved, "</sample>") + Len("</sample>"))
            
            str_项目 = "": str_样本号 = "": str_时间 = ""
            str_Img = "": strImgData = ""
            '<smpinfo></smpinfo>
            str_smpInfo = GetKeyVal(str_Data, "smpinfo")
            varline = Split(str_smpInfo, Chr(&HD) & Chr(&HA))
            
            For i = LBound(varline) To UBound(varline)
                str_Tmp = GetKeyVal(varline(i), "n")
                If str_Tmp = "SEQ" Then
                    str_样本号 = GetKeyVal(varline(i), "v")
                ElseIf str_Tmp = "DATE" Then
                    str_时间 = Replace(GetKeyVal(varline(i), "v"), "T", " ")
                End If
                If str_样本号 <> "" And str_时间 <> "" Then Exit For
            Next
            
            '<smpresults></smpresults>
            str_smpresults = GetKeyVal(str_Data, "smpresults")
            varline = Split(str_smpresults, Chr(&HD) & Chr(&HA))
            For i = LBound(varline) To UBound(varline) - 1
                If varline(i) <> "" And GetKeyVal(varline(i), "n") <> "" And GetKeyVal(varline(i), "v") <> "" Then
                    str_项目 = str_项目 & "|" & GetKeyVal(varline(i), "n") & "|" & GetKeyVal(varline(i), "v")
                End If
            Next
            str_项目 = Replace(str_项目, Chr(&HD) & Chr(&HA), "")
            
            '<hgrams></hgrams>
            str_hgrams = GetKeyVal(str_Data, "hgrams")
            For i = 0 To 2
                str_hgram = GetKeyVal(str_Data, "hgram")
                str_Data = DelStrRead(str_Data, "hgram")
                str_Tmp = GetKeyVal(str_hgram, "n")
                    
                If str_Tmp = "PLT" Then
                    str_Img = str_Img & "^PLT;0;0,80;"
                ElseIf str_Tmp = "RBC" Then
                    str_Img = str_Img & "^RBC;0;0,80;"
                ElseIf str_Tmp = "WBC" Then
                    str_Img = str_Img & "^WBC;6;0,80;"
                End If
        

                Do While GetKeyVal(str_hgram, "v") <> ""
                    strImgData = GetKeyVal(str_hgram, "v")
                    str_hgram = DelStrRead(str_hgram, "v")
                    '去除空格
                    strImgData = Replace(strImgData, " ", ";")
                    '取出回车换行
                    strImgData = Replace(strImgData, Chr(&HD) & Chr(&HA), ";")
                    strImgData = Mid(strImgData, 1, InStrRev(strImgData, ";") - 1)
                    strImgData = Mid(strImgData, InStr(strImgData, ";") + 1)
                    If str_Tmp = "WBC" Then strImgData = "~" & strImgData
                    str_Img = str_Img & strImgData
                Loop
            Next
            
            If Val(str_样本号) > 0 And IsDate(str_时间) And str_项目 <> "" Then
                strResult = strResult & "||" & str_时间 & "|" & str_样本号 & "| |血清|0" & str_项目
                If str_Img <> "" Then strResult = strResult & vbCrLf & Mid(str_Img, 2)
            End If
            
            If Not strReserved Like "*<sample>*</sample>*" Then
                blnEnd = True
            End If
        Loop
    Else
       strReserved = strSource
    End If
    If strResult <> "" Then
        If Left(strResult, 2) = "||" Then strResult = Mid(strResult, 3)
    End If
End Sub

Private Function GetKeyVal(ByVal strTemp As String, ByVal strKey As String) As String
    '根据 key 读取 VAL
    Dim int_Start As Integer
    Dim int_Length As Integer
    Dim str_KeyVal As String
    
    int_Start = InStr(strTemp, "<" & strKey & ">") + IIf(InStr(strTemp, "<" & strKey & ">") > 0, Len("<" & strKey & ">"), 0)
    If int_Start = 0 Then Exit Function
    int_Length = InStr(strTemp, "</" & strKey & ">") - int_Start
    str_KeyVal = Mid(strTemp, int_Start, int_Length)
    GetKeyVal = str_KeyVal
End Function

Private Function DelStrRead(ByVal strTemp As String, ByVal strKey As String) As String
    '删除已读取过的字符串
    Dim int_Start As Integer
    Dim int_Length As Integer
    Dim str_Result As String
    
    int_Start = InStr(strTemp, "</" & strKey & ">") + Len("</" & strKey & ">")
    If int_Start = 0 Then Exit Function
    str_Result = Mid(strTemp, int_Start)
    DelStrRead = str_Result
End Function

Public Function SendSample(ByVal strSample As String) As String
    '解析需要向仪器发送的标本信息
    '   元素之间以|分隔
    '   第0个元素：检验时间
    '   第1个元素：样本序号
    '   第2个元素：检验人
    '   第3个元素：标本
    '   第4个元素：是否质控品
    '   第5～9元素：系统保留
    '   从第10个元素开始为需要的检验项目。
    SendSample = ""
End Function
