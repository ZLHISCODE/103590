VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
  Persistable = 0  'NotPersistable
  DataBindingBehavior = 0  'vbNone
  DataSourceBehavior  = 0  'vbNone
  MTSTransactionMode  = 0  'NotAnMTSObject
END
Attribute VB_Name = "clsLISDev_File_DSFI1"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = True
Attribute VB_PredeclaredId = False
Attribute VB_Exposed = True
Option Explicit
'仪器:显微镜  DS-FI1 NIKON-55i
'厂商:尿康(NIKON)
'作者:陈东
'用户:德阳人民医院
Public Function GetStartCmd() As String
    '设备标准接口
    '功能：获取设备开始发送命令
    GetStartCmd = ""
End Function

Public Function GetEndCmd() As String
    '设备标准接口
    '功能：获取设备停止发送命令
    GetEndCmd = ""
End Function
Public Function ResultFromFile(ByVal strFile As String, ByVal lngSampleNO As Long, _
    ByVal dtStart As Date, Optional ByVal dtEnd As Date = CDate("3000-12-31")) As String()
'标准函数：从文件获取检验结果
'   strFile：包含路径的文件名
'   lngSampleNO：标本号。为-1表示选取所有时间范围内的标本
'   dtStart：开始时间。如果指定了标本号，则只提取该参数当天的这个标本（dtEnd无效）
'   dtEnd：结束时间。只有当选取多个标本（lngSampleNO=-1）时，该参数有效。如果不指定该参数值，则时间范围为>=dtStart。
'   返回的检验结果数组(各解析程序必须按以下标准组织结果)
'   元素之间以|分隔
'   第0个元素：检验时间
'   第1个元素：样本序号
'   第2个元素：检验人
'   第3个元素：标本
'   第4个元素：是否质控品
'   从第5个元素开始为检验结果，每2个元素表示一个检验项目。
'       如：第5i个元素为检验项目，第5i+1个元素为检验结果
    
    Dim aResults() As String
    Dim objFile As New Scripting.FileSystemObject
    Dim objStream As Scripting.TextStream, strLine As String

    Dim str日期 As String, str检验项目 As String, str标本号 As String
    Dim strFilePath As String, strFilename As String
     
    strFilePath = UCase(objFile.GetParentFolderName(strFile))
    strFilename = Dir(strFilePath & "\*.jpg")
    ReDim aResults(0)
    
    Do While strFilename <> ""
        If strFilename Like "########_*_*_*" Then
            str日期 = Mid(strFilename, 1, 8)
            str日期 = Mid(str日期, 1, 4) & "-" & Mid(str日期, 5, 2) & "-" & Mid(str日期, 7, 2)
            
            str标本号 = Trim(Split(strFilename, "_")(1))
            str检验项目 = Trim(Split(strFilename, "_")(2))
            If IsDate(str日期) And Val(str标本号) <> 0 And str检验项目 <> "" Then
                
                If CDate(str日期) >= dtStart And CDate(str日期) <= dtEnd Then
                    If lngSampleNO <> -1 Then
                        If lngSampleNO = Val(str标本号) Then
                            If aResults(UBound(aResults)) <> "" Then ReDim Preserve aResults(UBound(aResults) + 1)
                            aResults(UBound(aResults)) = str日期 & "|" & str标本号 & "| |血清|0|" & str检验项目 & "|　" & vbCrLf & "　;110;0;" & strFilePath & "\" & strFilename
                        End If
                    Else
                        If aResults(UBound(aResults)) <> "" Then ReDim Preserve aResults(UBound(aResults) + 1)
                        aResults(UBound(aResults)) = str日期 & "|" & str标本号 & "| |血清|0|" & str检验项目 & "|　" & vbCrLf & "　;110;0;" & strFilePath & "\" & strFilename
                    End If
                End If
            End If
        End If
        strFilename = Dir
    Loop
    ResultFromFile = aResults
End Function


