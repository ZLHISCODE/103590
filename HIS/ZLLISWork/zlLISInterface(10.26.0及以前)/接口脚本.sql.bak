Create Or Replace Procedure Zl_检验报告单_Insert
(
  Id_In   In 病人医嘱记录.ID%Type,
  Type_In In Number -- 0=新增 1=删除
) Is
  --HIS和其他LIS接口使用
  v_主页id     病人医嘱记录.主页id%Type;
  v_医嘱id     病人医嘱记录.ID%Type;
  v_开嘱科室id 病人医嘱记录.开嘱科室id%Type;
  v_病人来源   检验标本记录.病人来源%Type;
  v_病人id     检验标本记录.病人id%Type;
  v_婴儿       检验标本记录.婴儿%Type;
  v_病历文件id 病历单据应用.病历文件id%Type;
  v_病历文件名 病历文件列表.名称%Type;
  v_文件id     电子病历内容.文件id%Type;
  v_Temp       Varchar2(255);
  v_人员部门id 部门人员.部门id%Type;
  v_人员编号   人员表.编号%Type;
  v_人员姓名   人员表.姓名%Type;
  v_执行       Number;
  v_No         病人医嘱发送.NO%Type;
  v_性质       病人医嘱发送.记录性质%Type;
  v_序号       Varchar2(1000);

  --查找当前标本的相关申请
  Cursor c_Samplequest Is
    Select Distinct ID As 医嘱id From 病人医嘱记录 Where Id_In In (ID, 相关id);

  --未审核的费用行(不包含药品)
  Cursor c_Verify(v_医嘱id In Number) Is
    Select Distinct 记录性质, NO, 序号
    From 病人费用记录
    Where 收费类别 Not In ('5', '6', '7') And 医嘱序号 + 0 In (Select ID From 病人医嘱记录 Where v_医嘱id In (ID, 相关id)) And 记帐费用 = 1 And
          记录状态 = 0 And 价格父号 Is Null And
          (记录性质, NO) In (Select 记录性质, NO
                         From 病人医嘱附费
                         Where 医嘱id = v_医嘱id
                         Union All
                         Select 记录性质, NO
                         From 病人医嘱发送
                         Where 医嘱id In (Select ID From 病人医嘱记录 Where v_医嘱id In (ID, 相关id)))
    Order By 记录性质, NO, 序号;

  Cursor c_Stuff(v_No Varchar2, v_主页id Number) Is
    Select NO, 单据, 库房id
    From 未发药品记录
    Where NO = v_No And 单据 In (24, 25, 26) And 库房id Is Not Null And Not Exists
     (Select 1 From Dual Where Zl_Getsysparameter(Decode(v_主页id, Null, 92, 63)) = '1') And Exists
     (Select A.序号
           From 病人费用记录 A, 材料特性 B
           Where A.记录性质 = 2 And A.记录状态 = 1 And A.NO = v_No And A.收费细目id = B.材料id And B.跟踪在用 = 1)
    Order By 库房id;
Begin
  --操作员信息:部门ID,部门名称;人员ID,人员编号,人员姓名
  v_Temp       := Zl_Identity;
  v_人员部门id := To_Number(Substr(v_Temp, 1, Instr(v_Temp, ',') - 1));
  v_Temp       := Substr(v_Temp, Instr(v_Temp, ';') + 1);
  v_Temp       := Substr(v_Temp, Instr(v_Temp, ',') + 1);
  v_人员编号   := Substr(v_Temp, 1, Instr(v_Temp, ',') - 1);
  v_人员姓名   := Substr(v_Temp, Instr(v_Temp, ',') + 1);

  Select Nvl(B.主页id, 0), Nvl(B.相关id, 0), Decode(B.病人来源, 2, 2, 4, 4, 1), Nvl(B.病人id, 0), Nvl(B.开嘱科室id, 0), Nvl(B.婴儿, 0)
  Into v_主页id, v_医嘱id, v_病人来源, v_病人id, v_开嘱科室id, v_婴儿
  From 病人医嘱记录 B
  Where B.相关id = Id_In;

  Begin
    Select 病历文件id, C.名称
    Into v_病历文件id, v_病历文件名
    From 病人医嘱记录 A, 病历单据应用 B, 病历文件列表 C
    Where A.诊疗项目id = B.诊疗项目id And B.病历文件id = C.ID And A.相关id = v_医嘱id And B.应用场合 = v_病人来源 And Rownum <= 1;
  Exception
    When Others Then
      Return;
  End;

  If Type_In = 0 Then
    --新增
    --删除以前的报告记录
    Begin
      Select 病历id Into v_文件id From 病人医嘱报告 Where 医嘱id = v_医嘱id And Rownum <= 1;
      Delete 电子病历记录 Where ID = v_文件id;
      Delete 电子病历内容 Where 文件id = v_文件id;
    Exception
      When Others Then
        Select 电子病历记录_Id.Nextval Into v_文件id From Dual;
        --Insert Into 病人医嘱报告 (医嘱id, 病历id) Values (v_医嘱id, v_文件id);
    End;
  
    Insert Into 电子病历记录
      (ID, 病人来源, 病人id, 主页id, 婴儿, 科室id, 病历种类, 文件id, 病历名称, 创建人, 创建时间, 保存人, 保存时间, 最后版本, 签名级别)
    Values
      (v_文件id, v_病人来源, v_病人id, v_主页id, v_婴儿, v_开嘱科室id, 7, v_病历文件id, v_病历文件名, Null, Sysdate, Null, Sysdate, 1, 0);
  
    Insert Into 病人医嘱报告 (医嘱id, 病历id) Values (v_医嘱id, v_文件id);
  
    Insert Into 电子病历内容
      (ID, 文件id, 开始版, 终止版, 父id, 对象序号, 对象类型, 对象标记, 保留对象, 对象属性, 内容行次, 内容文本, 是否换行)
    Values
      (电子病历内容_Id.Nextval, v_文件id, 1, 1, Null, 1, 2, Null, Null, 0, 0, 0, 0);
  
    Update 病人医嘱发送 Set 执行状态 = 1 Where 医嘱id In (Select ID From 病人医嘱记录 Where v_医嘱id In (ID, 相关id));
  
    --执行后自动审核对应的记帐划价单(不包含药品)
    Select Zl_To_Number(Nvl(Zl_Getsysparameter(81), '0')) Into v_执行 From Dual;
    --2.检查当前标本相关的申请的相关标本是否完成审核
    For r_Samplequest In c_Samplequest Loop
    
      --r_SampleQuest.医嘱id申请已经完成,处理后续环节
    
      --2.费用执行处理
      Update 病人费用记录
      Set 执行状态 = 1, 执行时间 = Sysdate, 执行人 = v_人员姓名
      Where 收费类别 Not In ('5', '6', '7') And
            (医嘱序号, 记录性质, NO) In
            (Select 医嘱id, 记录性质, NO
             From 病人医嘱附费
             Where 医嘱id = r_Samplequest.医嘱id
             Union All
             Select 医嘱id, 记录性质, NO
             From 病人医嘱发送
             Where 医嘱id In (Select ID From 病人医嘱记录 Where r_Samplequest.医嘱id In (ID, 相关id)));
    
      --3.自动审核记帐
      If Nvl(v_执行, 0) = 1 Then
        For r_Verify In c_Verify(r_Samplequest.医嘱id) Loop
          If r_Verify.NO || ',' || r_Verify.记录性质 <> v_No || ',' || v_性质 Then
            If v_序号 Is Not Null Then
              If v_性质 = 1 Then
                Zl_门诊记帐记录_Verify(v_No, v_人员编号, v_人员姓名, Substr(v_序号, 2));
              Elsif v_性质 = 2 Then
                Zl_住院记帐记录_Verify(v_No, v_人员编号, v_人员姓名, Substr(v_序号, 2));
              End If;
            End If;
            v_序号 := Null;
          End If;
          v_No   := r_Verify.NO;
          v_性质 := r_Verify.记录性质;
          v_序号 := v_序号 || ',' || r_Verify.序号;
        End Loop;
        If v_序号 Is Not Null Then
          If v_性质 = 1 Then
            Zl_门诊记帐记录_Verify(v_No, v_人员编号, v_人员姓名, Substr(v_序号, 2));
          Elsif v_性质 = 2 Then
            Zl_住院记帐记录_Verify(v_No, v_人员编号, v_人员姓名, Substr(v_序号, 2));
          End If;
        End If;
      End If;
    
      --审核试剂消耗单
      v_No := Nextno(14);
    
      Update 检验试剂记录 Set NO = v_No Where 医嘱id = r_Samplequest.医嘱id;
    
      If v_No Is Not Null Then
      
        Zl_检验试剂记录_Bill(r_Samplequest.医嘱id, v_No);
      
        v_主页id := Null;
        Select 主页id Into v_主页id From 病人医嘱记录 A Where ID = r_Samplequest.医嘱id;
      
        If v_主页id Is Null Then
          Zl_门诊记帐记录_Verify(v_No, v_人员编号, v_人员姓名);
        Else
          Zl_住院记帐记录_Verify(v_No, v_人员编号, v_人员姓名);
        End If;
      
        --如果记帐没有自动发料,则自动发料,否则不处理
        For r_Stuff In c_Stuff(v_No, v_主页id) Loop
          Zl_材料收发记录_处方发料(r_Stuff.库房id, 25, v_No, v_人员姓名, v_人员姓名, v_人员姓名, 1, Sysdate);
        End Loop;
      End If;
    
    End Loop;
  Else
    --删除
    Select 病历id Into v_文件id From 病人医嘱报告 Where 医嘱id = v_医嘱id And Rownum <= 1;
    Delete 病人医嘱报告 Where 医嘱id = v_医嘱id;
    Delete 电子病历记录 Where ID = v_文件id;
    Delete 电子病历内容 Where 文件id = v_文件id;
    Update 病人医嘱发送 Set 执行状态 = 0 Where 医嘱id In (Select ID From 病人医嘱记录 Where v_医嘱id In (ID, 相关id));
    For r_Samplequest In c_Samplequest Loop
      --2.费用执行处理
      Update 病人费用记录
      Set 执行状态 = 0, 执行时间 = Null, 执行人 = Null
      Where 收费类别 Not In ('5', '6', '7') And
            (医嘱序号, 记录性质, NO) In
            (Select 医嘱id, 记录性质, NO
             From 病人医嘱附费
             Where 医嘱id = r_Samplequest.医嘱id
             Union All
             Select 医嘱id, 记录性质, NO
             From 病人医嘱发送
             Where 医嘱id In (Select ID From 病人医嘱记录 Where r_Samplequest.医嘱id In (ID, 相关id)));
    End Loop;
  End If;
Exception
  When Others Then
    Zl_Errorcenter(Sqlcode, Sqlerrm);
End Zl_检验报告单_Insert;
/
Create Or Replace Procedure Zl_电子病历格式_Insert
(
  Id_In   In 电子病历格式.文件id%Type,
  Txt_In  In Varchar2,
  开始_In In Number -- 1=开始
) Is
  l_Blob Blob;
Begin

  If 开始_In = 1 Then
    Delete 电子病历格式 Where 文件id = Id_In;
  End If;
  If 开始_In = 1 Then
    Update 电子病历格式 Set 内容 = Empty_Blob() Where 文件id = Id_In;
    If Sql%Rowcount = 0 Then
      Insert Into 电子病历格式 (文件id, 内容) Values (Id_In, Empty_Blob());
    End If;
  End If;
  Select 内容 Into l_Blob From 电子病历格式 Where 文件id = Id_In For Update;
  Dbms_Lob.Writeappend(l_Blob, Length(Hextoraw(Txt_In)) / 2, Hextoraw(Txt_In));
Exception
  When Others Then
    Zl_Errorcenter(Sqlcode, Sqlerrm);
End Zl_电子病历格式_Insert;
/
CREATE OR REPLACE Procedure Zl_检验医嘱标记_Edit
(
  Id_In   In 病人医嘱记录.ID%Type,
  Type_In In Number -- 1=核收 0=取消核收
) Is
Begin
  Update 病人医嘱发送 Set 执行状态 = Type_In Where 医嘱id In (Select ID From 病人医嘱记录 Where Id_In In (ID, 相关id));
Exception
  When Others Then
    Zl_Errorcenter(Sqlcode, Sqlerrm);
End Zl_检验医嘱标记_Edit;
/